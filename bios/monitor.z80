BUFFER		= 0x2000
BACKSPACE	= 8
CR              = 10
ENTER		= 13

start:
	ld	hl, BUFFER
	ld      a, '*'			; print prompt
	out     (UART), a
	ld      a, ' '
	out     (UART), a

nextchr:
	in	a, (UART_CONFIG)	; key pressed?
	and	UART_INPUT_MSK
	jr	z, nextchr

	in	a, (UART)		; get key pressed

	cp	ENTER  			; is ENTER?
	jr	z, enter

	cp	BACKSPACE		; is BACKSPACE?
	jr	z, backspace

	ld	b, a			; check if the line has less than 127 characters
	ld	a, l
	cp	128
	jr	c, .has_space
	jr	nextchr
.has_space:
	ld	a, b

	out	(UART), a		; print key pressed
	ld	(hl), a			; add to buffer
	inc	hl
	jr	nextchr


backspace:
	ld	a, l			; check if L == 0, return
	or	a
	jr	z, nextchr

	ld	a, BACKSPACE		; erase last char
	out	(UART), a
	ld	a, ' '
	out	(UART), a
	ld	a, BACKSPACE
	out	(UART), a
	dec	hl			; decrement buffer
	jr	nextchr


enter:
	ld	a, CR			; print ENTER
	out	(UART), a
	ld	a, ENTER
	out	(UART), a

	jr	start


; vim:ts=8:sw=8:sts=8:noexpandtab
